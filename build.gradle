plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.190'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
    id "co.uzzu.dotenv.gradle" version "1.1.0"
}

tasks.named('wrapper', Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    // Switching this to Wrapper.DistributionType.ALL will download the full gradle sources that comes with
    // documentation attached on cursor hover of gradle classes and methods. However, this comes with increased
    // file size for Gradle. If you do switch this to ALL, run the Gradle wrapper task twice afterwards.
    // (Verify by checking gradle/wrapper/gradle-wrapper.properties to see if distributionUrl now points to `-all`)
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    // Add here additional repositories if required by some of the dependencies below.
}

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

//minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')
//minecraft.accessTransformers.entry public net.minecraft.client.Minecraft textureManager # textureManager

// Default run configurations.
// These can be tweaked, removed, or duplicated as needed.
runs {
    // applies to all the run configs below
    configureEach {
        // Recommended logging data for a userdev environment
        // The markers can be added/remove as needed separated by commas.
        // "SCAN": For mods scan.
        // "REGISTRIES": For firing of registry events.
        // "REGISTRYDUMP": For getting the contents of all registries.
        systemProperty 'forge.logging.markers', 'REGISTRIES'

        // Recommended logging level for the console
        // You can set various levels here.
        // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        argument '--nogui'
    }

    // This run config launches GameTestServer and runs all registered gametests, then exits.
    // By default, the server will crash when no gametests are provided.
    // The gametest system is also enabled by default for other run configs under the /test command.
    gameTestServer {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
        // workingDirectory project.file('run-data')

        // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
        arguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
}
repositories {
    maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel
    maven { url = "https://mvn.devos.one/snapshots" } // Registrate
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // ForgeConfigAPIPort
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"

    implementation("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }
    implementation("net.createmod.ponder:Ponder-NeoForge-${minecraft_version}:${ponder_version}")
    compileOnly("dev.engine-room.flywheel:flywheel-neoforge-api-${minecraft_version}:${flywheel_version}")
    runtimeOnly("dev.engine-room.flywheel:flywheel-neoforge-${minecraft_version}:${flywheel_version}")
    implementation("com.tterrag.registrate:Registrate:${registrate_version}")
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

def getRelevantChangelog(String version) {
    def changelogFile = file("CHANGELOG.md")
    if (!changelogFile.exists()) {
        println("Couldn't read changelog for version " + version + ", file does not exist.")
        throw new GradleException("Changelog file CHANGELOG.md does not exist.")
    }

    // 1. Read the file
    Map<String, String> versionChangelogMap = [:] as LinkedHashMap
    def currentBlockVersion = null
    def currentBlockLines = []

    changelogFile.eachLine { line ->
        def headerDefinition = "# "
        if (line.startsWith(headerDefinition)) {
            if (currentBlockVersion != null) {
                versionChangelogMap[currentBlockVersion] = currentBlockLines.join("\n").trim()
            }
            currentBlockVersion = line.substring(headerDefinition.length()).trim()
            currentBlockLines = []
        } else if (currentBlockVersion != null) {
            currentBlockLines << line
        }
    }
    if (currentBlockVersion != null) {
        versionChangelogMap[currentBlockVersion] = currentBlockLines.join("\n").trim()
    }

    def cleanVersion = version.trim()
    def log = versionChangelogMap.get(cleanVersion)

    if (log == null) {
        throw new GradleException("Couldn't find changelog for version " + version)
    }

    // --- 2. Smart Comparison Logic ---

    // Helper: Split version into parts (e.g., "1.21-0.3jolly" -> [1, 21, 0, 3])
    // This extracts only numbers for comparison to avoid "9 > 33" text issues.
    def getVersionParts = { String v ->
        return v.split(/[-.]/).collect { part ->
            // Extract the first number found in the segment (handles "33jolly" -> 33)
            def matcher = (part =~ /^(\d+)/)
            return matcher.find() ? matcher[0][1].toInteger() : -1
        }
    }

    // Helper: Returns true if vCandidate > vTarget
    def isNewer = { String vCandidate, String vTarget ->
        def p1 = getVersionParts(vCandidate)
        def p2 = getVersionParts(vTarget)

        // Compare segment by segment
        for (int i = 0; i < Math.min(p1.size(), p2.size()); i++) {
            if (p1[i] != p2[i]) {
                return p1[i] > p2[i]
            }
        }
        return p1.size() > p2.size() // If all match, longer one is "newer" (usually)
    }

    // Helper: Extract MC version (assumes format MC-MOD or similar)
    def getMcPrefix = { String v -> v.split("-")[0] }
    def currentMcVersion = getMcPrefix(cleanVersion)

    // --- 3. Check for Newer Versions ---

    // Filter map keys to find warnings
    def newerVersions = versionChangelogMap.keySet().findAll { candidateVersion ->
        // Only check if candidate is distinct from current
        if (candidateVersion == cleanVersion) return false

        // strict check: Must be same Minecraft version
        if (getMcPrefix(candidateVersion) != currentMcVersion) return false

        // Check if candidate is actually newer numerically
        return isNewer(candidateVersion, cleanVersion)
    }

    if (!newerVersions.isEmpty()) {
        println "\n**************************************************"
        println " WARNING: You are generating a changelog for ${cleanVersion}, but newer version(s) were detected for this MC version:"
        newerVersions.each { println "  - ${it}" }
        println "**************************************************\n"
    }

    return log
}

//publishMods {
//    file = jar.archiveFile
//    def changelogText = getRelevantChangelog(minecraft_version + "-" + mod_version)
//    changelog = changelogText
//    println("Using changelog:\n" + changelogText + "\n")
//    type = STABLE
//    modLoaders.add("neoforge")
//    displayName = mod_name + " " + mod_version + " NeoForge " + minecraft_version
//
//    curseforge {
//        projectId = "1398383"
//        projectSlug = "create-bits-n-bobs"
//        accessToken = env.fetch("CURSEFORGE_TOKEN")
//        minecraftVersions.add("1.21.1")
//        requires("create")
//    }
//    modrinth {
//        projectId = "T8bvmqVZ"
//        accessToken = env.fetch("MODRINTH_TOKEN")
//        minecraftVersions.add("1.21.1")
//        requires("create")
//    }
//
//    discord {
//        webhookUrl = env.fetch("DISCORD_WEBHOOK_URL")
//        username = "bits n bober version bot"
//        avatarUrl = "https://cataas.com/cat?width=500&height=500"
//        content = """
//        # New Mod Release: ${getDisplayName().get()}
//        ${changelogText}
//        """.trim()
//        style {
//            look = "CLASSIC"
//        }
//    }
//}